(mdalconfig
 version: 2 target: spectrum48
 description: "A simple 2 channel pin pulse (PFM) engine in less than 100 bytes.
 By Shiru 2011, 2013."

 commands: ((command id: BPM bits: 16 type: uint default: 140)
	    (command id: NOTE bits: 8 type: ukey
		     tags: (enable_modifiers use_last_set is_note)
		     keys: (make-dividers 118 8 0 -4)
		     default: "rest")
	    (command id: DRUM type: trigger default: #f description:
		     "Trigger a click drum. Replaces note on channel 1."))

 input: ((field from: BPM)
	 (group id: PATTERNS flags: (ordered)
		nodes: ((block id: DRUMS nodes: ((field from: DRUM)))
			(clone 2 (block id: CH
					nodes: ((field from: NOTE)))))))

 output: ((asm file: "huby.asm")
	  (comment "sequence")
	  (field bytes: 2 compose: (quotient 1779661 ?BPM))
	  (field bytes: 2 compose: (- $sequence_end 8))
	  (order from: (CH1, CH2) layout: shared-numeric-matrix base-index: 1)
	  (field bytes: 1 compose: 0)
	  (symbol id: sequence_end)
	  (block id: CH1 from: (CH1, DRUMS) resize: 8 list: order
		 nodes: ((field bytes: 1 compose: (if (is-set? ?DRUM)
						      #x2c ?NOTE1))))
	  (block id: CH2 from: (CH2) resize: 8 list: order
		 nodes: ((field bytes: 1 compose: ?NOTE2)))))
